if(NOT PYTHON_EXECUTABLE)
    find_package(PythonInterp 3.5 REQUIRED QUIET)
endif()

set(CONFIG_GENERATOR_PATH "@GENERATOR_INSTALL_PATH@/ConfigGenerator")
set(CONFIG_GENERATOR "${CONFIG_GENERATOR_PATH}/config_generator.py")
set(CONFIG_GENERATOR_PARAMS_WHITELIST "${CONFIG_GENERATOR_PATH}/params.config")

macro(IncludeConfig)
    if(NOT COMMAND map)
        ## includes all cmake files of cmakepp to write json files
        include("${MODULE_BASE_DIR}/config/core/require.cmake")
        require("${MODULE_BASE_DIR}/config/*.cmake")
    endif()
endmacro()


# ----------------------------------------------------------------------------------------
# write_config
# ----------------------------------------------------------------------------------------
# Writes and installs a json configs for a plugin.
#
#    optional:
#    PLUGINS     List of the config files to write without extention defaults to
#                PROJECT_NAME only
#                e.g. "UX" will generate and install UX.json for the WebkitBrowser plugin.
#    CLASSNAME   Name of the package, defaults to PROJECT_NAME of
#                the latest cmake project call.
#    LOCATOR     Version of the package, defaults to lib + MODULE_NAME + CMAKE_SHARED_LIBRARY_SUFFIX
#                of the latest cmake project call.
#    COMPONENT   The cmake component group defaults to PROJECT_NAME
# ----------------------------------------------------------------------------------------
function(write_config)
    set(optionsArgs )
    set(oneValueArgs CLASSNAME LOCATOR COMPONENT)
    set(multiValueArgs PLUGINS)

    if (NOT CONFIG_GENERATOR)
        message(FATAL_ERROR "The path CONFIG_GENERATOR is not set!")
    endif()

    if(NOT EXISTS "${CONFIG_GENERATOR}" OR IS_DIRECTORY "${CONFIG_GENERATOR}")
        message(FATAL_ERROR "ConfigGenerator path ${CONFIG_GENERATOR} invalid.")
    endif()

    cmake_parse_arguments(ARG "${optionsArgs}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN} )

    if(ARG_UNPARSED_ARGUMENTS)
        if(ARG_PLUGINS)
            message(FATAL_ERROR "Unknown keywords given to write_config: \"${ARG_UNPARSED_ARGUMENTS}\"")
        else()
            if ("${ARG_UNPARSED_ARGUMENTS}" STREQUAL "${PROJECT_NAME}")
                set(_fix "write_config\(\)")
            else()
                set(_fix "write_config\(PLUGINS ${ARG_UNPARSED_ARGUMENTS}\)")
            endif()
            message(DEPRECATION "======DEPRECATION=DETECTED==================================================\nPROPOSED FIX:\n${CMAKE_CURRENT_LIST_FILE}\n\"write_config(${ARGN})\" --> \"${_fix}\"\n\============================================================================")
            set(_plugins ${ARG_UNPARSED_ARGUMENTS})
            set(_classname ${PLUGIN_NAME})
            set(_component ${ARG_UNPARSED_ARGUMENTS})
            message(STATUS "Assuming:")
            message(STATUS "Plugin: ${_plugins}")
            message(STATUS "Classname: ${_classname}")
            message(STATUS "Component: ${_component}")
        endif()
    endif()

    if(NOT _component AND NOT _plugins AND NOT _classname)
    if(ARG_CLASSNAME)
        set(_classname ${ARG_CLASSNAME})
    else()
        set(_classname ${PROJECT_NAME})
    endif()

    if(ARG_PLUGINS)
        set(_plugins ${ARG_PLUGINS})
    else()
        set(_plugins ${PROJECT_NAME})
    endif()

    if(ARG_COMPONENT)
        set(_component ${ARG_COMPONENT})
    else()
        set(_component ${PROJECT_NAME})
    endif()
    endif()

    if(ARG_LOCATOR)
        set(_locator ${ARG_LOCATOR})
    else()
        set(_locator lib${MODULE_NAME}${CMAKE_SHARED_LIBRARY_SUFFIX})
    endif()

    IncludeConfig()

    foreach(plugin ${_plugins})
        set(config_generated "N")
        if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/${plugin}.conf.in")
            message(STATUS "Writing configuration for ${plugin} using config_generator")

            set(_execute_command "${CONFIG_GENERATOR}")

            list(APPEND _execute_command  "-l" "${_locator}")
            list(APPEND _execute_command  "-c" "${_classname}")
            list(APPEND _execute_command  "-o" "${CMAKE_CURRENT_BINARY_DIR}/config/${plugin}.json")
            list(APPEND _execute_command  "-p" "${CONFIG_GENERATOR_PARAMS_WHITELIST}")
            list(APPEND _execute_command  "--indent" 1)

            file(MAKE_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/config")

            if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/${plugin}.conf.in")
                configure_file( "${CMAKE_CURRENT_LIST_DIR}/${plugin}.conf.in"
                    "${CMAKE_CURRENT_BINARY_DIR}/config/${plugin}.conf"
                    @ONLY)
                list(APPEND _execute_command  "-i" "${plugin}.conf")
            endif()

            list(APPEND _execute_command  ${plugin} "${CMAKE_CURRENT_BINARY_DIR}/config")

            execute_process(COMMAND ${PYTHON_EXECUTABLE} ${_execute_command} RESULT_VARIABLE rv)
            if(NOT ${rv} EQUAL 0)
                message(FATAL_ERROR "config_generator failed.")
                return()
            endif()
            set(config_generated "Y")
        endif()

        if("${CMAKE_BUILD_TYPE}" STREQUAL "Debug" OR
                "${config_generated}" STREQUAL "N")

            message(STATUS "Writing configuration for ${plugin} using Legacy Config writer")

            map()
            kv(locator ${_locator})
            kv(classname ${_classname})
            end()
            ans(plugin_config) # default configuration
            
            if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/${plugin}.config")
                include(${CMAKE_CURRENT_LIST_DIR}/${plugin}.config)

                list(LENGTH preconditions number_preconditions)
                if (number_preconditions GREATER 0)
                    map_append(${plugin_config} precondition ___array___)
                    foreach(entry ${preconditions})
                        map_append(${plugin_config} precondition ${entry})
                    endforeach()
                endif()

                list(LENGTH terminations number_terminations)
                if (number_terminations GREATER 0)
                    map_append(${plugin_config} termination ___array___)
                    foreach(entry ${terminations})
                        map_append(${plugin_config} termination ${entry})
                    endforeach()
                endif()

                if (NOT ${callsign} STREQUAL "")
                    map_append(${plugin_config} callsign ${callsign})
                endif()

                if (NOT ${autostart} STREQUAL "")
                    map_append(${plugin_config} autostart ${autostart})
                endif()

                if (NOT ${resumed} STREQUAL "")
                    map_append(${plugin_config} resumed ${resumed})
                endif()

                if (NOT ${persistentpathpostfix} STREQUAL "")
                    map_append(${plugin_config} persistentpathpostfix ${persistentpathpostfix})
                    unset(persistentpathpostfix)
                endif()

                if (NOT ${configuration} STREQUAL "")
                    map_append(${plugin_config} configuration ${configuration})
                endif()
            endif()

            if ("${config_generated}" STREQUAL "N")
                json_write("${CMAKE_CURRENT_BINARY_DIR}/config/${plugin}.json" ${plugin_config})
            endif()

            if("${CMAKE_BUILD_TYPE}" STREQUAL "Debug" AND
                    "${config_generated}" STREQUAL "Y")

                json_write("${CMAKE_CURRENT_BINARY_DIR}/config/${plugin}_legacy.json" ${plugin_config})

                if(EXISTS "${CMAKE_CURRENT_BINARY_DIR}/config/${plugin}_legacy.json" AND 
                        EXISTS ${CMAKE_CURRENT_BINARY_DIR}/config/${plugin}.json)

                    execute_process( COMMAND ${CMAKE_COMMAND} -E compare_files ${CMAKE_CURRENT_BINARY_DIR}/config/${plugin}_legacy.json ${CMAKE_CURRENT_BINARY_DIR}/config/${plugin}.json
                     RESULT_VARIABLE compare_result
                    )

                    if(compare_result EQUAL 0)
                        message(STATUS "config json files generated using legacy method and Python ConfigGenerator are identical") 
                    elseif( compare_result EQUAL 1)
                        message(FATAL_ERROR " Comparing the config json files generated using legacy method and Python ConfigGenerator: \n"
                                            " ${CMAKE_CURRENT_BINARY_DIR}/config/${plugin}_legacy.json and ${CMAKE_CURRENT_BINARY_DIR}/config/${plugin}.json are not identical \n"
                                            " This could mean one of the following: \n"
                                            "    1. Parameters are out of order \n"
                                            "    2. Parameters are wrong. e.g, \"true\" intead of true \n")
                    else()
                        message(STATUS "Error while comparing the config json files generated using legacy method and Python ConfigGenerator")
                    endif()
                else()
                    message(STATUS "Failed to compare the config json files generated using legacy method and Python ConfigGenerator - missing")
                endif()
            endif()
        endif()

        install(
                FILES ${CMAKE_CURRENT_BINARY_DIR}/config/${plugin}.json DESTINATION
               "${CMAKE_INSTALL_PREFIX}/../etc/${NAMESPACE}/plugins/"
                COMPONENT ${_component})


    endforeach()


    unset(_plugins)
    unset(_classname)
    unset(_component)

endfunction(write_config)

